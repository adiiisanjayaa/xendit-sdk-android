apply plugin: 'com.android.library'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

group 'com.xendit'
version '2.3.4'

ext {
    bintrayOrg = 'xendit'
    bintrayRepo = 'android'
    bintrayName = 'xendit-sdk-android'

    publishedGroupId = this.group
    libraryName = 'Xendit'
    artifact = 'xendit-android'

    libraryDescription = 'Xendit makes it simple to collect credit card information securely, without it having to touch your server'

    siteUrl = 'https://github.com/xendit/xendit-sdk-android'
    gitUrl = 'https://github.com/xendit/xendit-sdk-android.git'

    libraryVersion = this.version

    developerId = '0505gonzalez'
    developerName = 'Juan Gonzalez.'
    developerEmail = 'juan@xendit.co'

    licenseName = 'MIT'
    licenseUrl = ''
    allLicenses = ["MIT"]
}

android {
    compileSdkVersion 28
    buildToolsVersion '28.0.3'
    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 28
        versionCode 1
        versionName
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    productFlavors {
    }
    testOptions {
        unitTests.returnDefaultValues = true
        unitTests.includeAndroidResources = true
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'com.android.volley:volley:1.1.1'
    implementation group: 'com.google.code.gson', name: 'gson', version: '2.8.5'
    implementation 'com.android.support:support-annotations:28.0.0'
    implementation 'com.android.support:support-compat:28.0.0'

    // sentry
    api 'io.sentry:sentry-android:1.7.10'

    // this dependency is not required if you are already using your own
    // slf4j implementation
    api 'org.slf4j:slf4j-nop:1.7.25'

    // Testing-only dependencies
    // Required -- JUnit 4 framework
    testImplementation 'junit:junit:4.12'
    // Optional -- Robolectric environment
    testImplementation 'androidx.test:core:1.2.0'
    testImplementation 'org.robolectric:robolectric:4.0-beta-1'
    testImplementation "com.google.truth:truth:0.42"
    androidTestImplementation "org.mockito:mockito-core:2.28.2"
    androidTestImplementation "com.google.truth:truth:0.42"
    androidTestImplementation 'com.android.support.test:rules:1.0.2'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'

    // retrofit
    api 'com.squareup.retrofit2:retrofit:2.5.0'
    api 'com.squareup.retrofit2:converter-gson:2.5.0'
    api 'com.squareup.retrofit2:converter-scalars:2.5.0'
    //Logging Network Calls
    api 'com.squareup.okhttp3:logging-interceptor:3.6.0'

    // snowplow
    api 'com.snowplowanalytics:snowplow-android-tracker:1.7.1'
}

publishing {
    publications {
        Production(MavenPublication) {
            artifact("$buildDir/outputs/aar/xendit-android-release.aar") // replace with your library name
            groupId this.group
            artifactId 'xendit-android' // replace with your library artifact id
            version this.version

            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')

                // Iterate over the implementation dependencies (we don't want the test ones),
                // adding a <dependency> node for each
                configurations.implementation.allDependencies.each {
                    // Ensure dependencies such as fileTree are not included in the pom.
                    if (it.name != 'unspecified') {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
    }
}

tasks.withType(Javadoc).all { enabled = false }

apply from: 'https://raw.githubusercontent.com/xendit/xendit-sdk-android/master/installv1.gradle'
apply from: 'https://raw.githubusercontent.com/xendit/xendit-sdk-android/master/bintrayv1.gradle'
